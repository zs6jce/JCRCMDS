      *----------------------------------------------------------------
      * JCRDUMPD - Dump count by program - DSPF
      *   Craig Rutledge  < www.jcrcmds.com >
      *----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      CA03 CA05 CA06 CA12 CA21
     A                                      INDARA PRINT
     A          R SBFDTA1                   SFL
     A            SSPLFNAM      10A  H
     A            SSPLFNBR       6A  H
     A            SBFOPTION      1A  B  5  3
     A            SPGMNAM       10A  O  5  6
     A            SPGMLIB       10A  O  5 17
     A            SUSERNAM      10A  O  5 28
     A            SJOBNAM       10A  O  5 39
     A            SJOBNBR        6A  O  5 51
     A            SSDATE          L  O  5 59DATFMT(*ISO)
     A            SSTIME          T  O  5 70TIMFMT(*HMS)
      *----------------------------------------------------------------
     A          R SBFCTL1                   SFLCTL(SBFDTA1) OVERLAY
     A  *DS3                                SFLPAG(17) SFLSIZ(34)
     A  *DS4                                SFLPAG(17) SFLSIZ(34)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A N32                                  SFLCLR
     A N34                                  SFLEND(*MORE)
     A                                  1  3'JCRDUMPR3' COLOR(BLU)
     A                                  1 23'Dump Spooled File Viewer'
     A                                      DSPATR(HI)
     A            SCDOW          9A  O  1 62COLOR(BLU)
     A                                  1 72DATE EDTCDE(Y) COLOR(BLU)
     A                                  2 72SYSNAME COLOR(BLU)
     A                                  3  3'1=SndNet' COLOR(BLU)
     A                                  3 12'2=Change Outq' COLOR(BLU)
     A                                  3 26'5=Display' COLOR(BLU)
     A                                  4  2'Opt' DSPATR(HI UL)
     A                                  4  6'Program   ' DSPATR(HI UL)
     A                                  4 17'Lib       ' DSPATR(HI UL)
     A                                  4 28'User      ' DSPATR(HI UL)
     A                                  4 39'Job       ' DSPATR(HI UL)
     A                                  4 51'Number' DSPATR(HI UL)
     A                                  4 59'Date      '  DSPATR(HI UL)
     A                                  4 70'Time    '  DSPATR(HI UL)
      *----------------------------------------------------------------
     A          R SFOOTER1
     A                                      OVERLAY
     A                                 23  2'F3=Exit' COLOR(BLU)
     A                                 23 14'F5=Refresh' COLOR(BLU)
     A                                 23 30'F6=Print' COLOR(BLU)
     A                                 23 45'F21=Command Line' COLOR(BLU)
     A                                 23 69'F12=Cancel' COLOR(BLU)
      *----------------------------------------------------------------
     A          R MSGSFL                    SFL SFLMSGRCD(24)
     A            MSGSFLKEY                 SFLMSGKEY
     A            PROGID                    SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N14                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(2)
     A            PROGID                    SFLPGMQ(10)
