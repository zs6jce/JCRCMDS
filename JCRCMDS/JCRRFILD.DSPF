      *----------------------------------------------------------------
      * JCRRFILD - File Record Format xref for RPG source - DSPF
      *   Craig Rutledge  < www.jcrcmds.com >
      *----------------------------------------------------------------
     A                                      DSPSIZ(27 132 *DS4)
     A                                      INDARA
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA07
     A                                      CA09
     A                                      CA12
     A          R SBFDTA1                   SFL
     A            HIDRCDFMT     10A  H
     A            HIDLIBNAM     10A  H
     A            HIDFILEEXT    10A  H
     A            SBFOPTION      1Y 0B  5  5EDTCDE(4)
     A            SCRFILENAM    10A  O  5  7
     A            SCRLIB        10A  O  5 18
     A            SCRRCDFMT     10A  O  5 29
     A            SCRBASEDPF    10A  O  5 40
     A            SCRRENAMED    10A  O  5 51
     A            SCRUSAGE       1A  O  5 63
     A            SCRAADD        1   O  5 67
     A            SCRRECTEXT    50A  O  5 70
      *----------------------------------------------------------------
     A          R SBFCTL1                   SFLCTL(SBFDTA1)
     A                                      SFLSIZ(0060)
     A                                      SFLPAG(0020)
     A                                      BLINK
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A N32                                  SFLCLR
     A N34                                  SFLEND(*MORE)
     A            SFLRCDNBR      4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'JCRRFIL'
     A                                      COLOR(BLU)
     A                                  1 23'File Record Format Xref'
     A                                      DSPATR(HI)
     A            SCDOW          9A  O  1 62COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2'Mbr:'
     A            SCOBJHEAD     64A  O  2  7
     A                                  2 72SYSNAME
     A                                      COLOR(BLU)
     A            SCHEADOPT     65A  O  3  5COLOR(BLU)
     A                                  4  7'File'
     A                                      DSPATR(HI)
     A                                  4 18'Lib'
     A                                      DSPATR(HI)
     A                                  4 29'RcdFmt'
     A                                      DSPATR(HI)
     A                                  4 40'PF'
     A                                      DSPATR(HI)
     A                                  4 51'Renamed'
     A                                      DSPATR(HI)
     A                                  4 62'Use'
     A                                      DSPATR(HI)
     A                                  4 66'Add'
     A                                      DSPATR(HI)
     A                                  4 70'Text'
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R SFOOTER1
     A                                      OVERLAY
     A                                 26  2'F3=Exit'
     A                                      COLOR(BLU)
      *                                26 15'F5=Refresh' COLOR(BLU)
     A            FOOTMSG07     16A  O 26 13COLOR(BLU)
     A            FOOTMSG09     26A  O 26 36COLOR(BLU)
     A                                 26 69'F12=Cancel'
     A                                      COLOR(BLU)
      *----------------------------------------------------------------
     A          R MSGSFL                    SFL SFLMSGRCD(27)
     A            MSGSFLKEY                 SFLMSGKEY
     A            PROGID                    SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N14                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(2)
     A            PROGID                    SFLPGMQ(10)
