      *----------------------------------------------------------------
      * JCROLCKD - Object lock list-sndbrkmsg or endjob(*immed) - DSPF
      *   Craig Rutledge  < www.jcrcmds.com >
      *----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      INDARA CA03 CA05 CA08 CA10 CA12
     A          R SBFDTA1                   SFL
     A            SCJOBNUMB      6A  H
     A            SBFOPTION      2Y 0B  7  2EDTCDE(4)
     A            SCJOBNAME     10A  O  7  5
     A            SCJOBUSER     10A  O  7 16
     A            SCDS          48   O  7 27
      *----------------------------------------------------------------
     A          R SBFCTL1                   SFLCTL(SBFDTA1) OVERLAY BLINK
     A  *DS3                                SFLPAG(15) SFLSIZ(60)
     A  *DS4                                SFLPAG(15) SFLSIZ(60)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A N32                                  SFLCLR
     A N34                                  SFLEND(*MORE)
     A            SFLRCDNBR      4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'JCROLCK' COLOR(BLU)
     A                                  1 23'Jobs With Object Locks' DSPATR(HI)
     A            SCDOW          9A  O  1 62COLOR(BLU)
     A                                  1 72DATE EDTCDE(Y) COLOR(BLU)
     A                                  2  2'Object:' DSPATR(HI)
     A            SCOBJHEAD     61A  O  2 10
     A                                  2 72SYSNAME COLOR(BLU)
     A                                  4  2'1=Sndbrkmsg' COLOR(BLU)
     A                                  4 15'2=Chgjob' COLOR(BLU)
     A                                  4 25'3=Strsrvjob' COLOR(BLU)
     A                                  4 38'4=Endjob' COLOR(BLU)
     A                                  4 48'5=Dspjob' COLOR(BLU)
     A                                  4 58'8=Wrksplf' COLOR(BLU)
     A                                  4 69'9=File I/O' COLOR(BLU)
     A                                  5  2'10=Strdbg' COLOR(BLU)
     A                                  5 24'15=Endsrvjob' COLOR(BLU)
     A                                  5 37'11=Dspusrprf' COLOR(BLU)
     A                                  5 57'20=Enddbg' COLOR(BLU)
     A                                  6  5'Job' DSPATR(HI)
     A                                  6 16'User' DSPATR(HI)
     A            SCTOGGHEAD    48   O  6 27DSPATR(HI)
      *----------------------------------------------------------------
     A          R SFOOTER1
     A                                      OVERLAY
     A                                 23  2'F3=Exit' COLOR(BLU)
     A                                 23 11'F5=Refresh' COLOR(BLU)
     A                                 23 23'F8=Toggle Lock/User Info'
     A                                      COLOR(BLU)
     A                                 23 49'F10=Set Break Msg' COLOR(BLU)
     A                                 23 69'F12=Cancel' COLOR(BLU)
      *----------------------------------------------------------------
     A          R WINDOWB
     A  *DS3                                WINDOW(5 2 5 73 *NOMSGLIN)
     A  *DS4                                WINDOW(5 2 5 73 *NOMSGLIN)
     A                                      WDWTITLE((*TEXT 'Override Break Mes-
     A                                      sage') (*COLOR WHT) (*DSPATR HI))
     A                                      OVERLAY
     A                                  1  2'Enter Break Message'
     A                                      COLOR(PNK)
     A            WBBRKMSG      70A  B  3  2CHECK(LC)
     A                                  5  2'Enter=Accept' COLOR(BLU)
     A                                  5 21'F12=Cancel' COLOR(BLU)
      *----------------------------------------------------------------
     A          R MSGSFL                    SFL SFLMSGRCD(24)
     A            MSGSFLKEY                 SFLMSGKEY
     A            PROGID                    SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N14                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(2)
     A            PROGID                    SFLPGMQ(10)
