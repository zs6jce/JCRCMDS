      *----------------------------------------------------------------
      * JCRLOGD - Retrieve previously executed commands - DSPF
      *   Craig Rutledge  < www.jcrcmds.com >
      *----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      INDARA CA03 CA05 CA12 CA21
     A          R SBFDTA1                   SFL
     A            ACMDMSG        1A  P
     A            FULLCMD      500A  H
     A            SBFOPTION      1A  B  4  2
     A            CMDMSG73      73A  O  4  4DSPATR(&ACMDMSG)
      *----------------------------------------------------------------
     A          R SBFCTL1                   SFLCTL(SBFDTA1) OVERLAY
     A                                      SFLPAG(18) SFLSIZ(72)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A N32                                  SFLCLR
     A N34                                  SFLEND(*MORE)
     A            S1RECNUM       4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  3'JCRLOG' COLOR(BLU)
     A                                  1 23'Previously Executed Commands'
     A                                      DSPATR(HI)
     A            SCDOW          9A  O  1 62COLOR(BLU)
     A                                  1 72DATE EDTCDE(Y) COLOR(BLU)
     A                                  2 72SYSNAME COLOR(BLU)
     A                                  3  2'1=Run' COLOR(BLU)
     A                                  3  8'2=Prompt' COLOR(BLU)
      *----------------------------------------------------------------
     A          R SFOOTER1                  OVERLAY BLINK
     A                                 23  2'F3=Exit' COLOR(BLU)
     A                                 23 20'F5=Refresh' COLOR(BLU)
     A                                 23 46'F21=Command Line' COLOR(BLU)
     A                                 23 69'F12=Cancel' COLOR(BLU)
      *----------------------------------------------------------------
     A          R MSGSFL                    SFL SFLMSGRCD(24)
     A            MSGSFLKEY                 SFLMSGKEY
     A            PROGID                    SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N14                                  SFLEND
     A                                      SFLPAG(1) SFLSIZ(2)
     A            PROGID                    SFLPGMQ(10)
