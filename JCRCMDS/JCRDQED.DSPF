      *----------------------------------------------------------------
      * JCRDQED - Data Queue Entries Display - DSPF
      *   Craig Rutledge  < www.jcrcmds.com >
      *----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      CA10
     A                                      CA11
     A                                      CA12
     A                                      CA19
     A                                      CA20
     A                                      PAGEUP
     A                                      PAGEDOWN
     A                                      INDARA
     A                                      PRINT
     A  04                                  CA14
     A          R SHEADER
     A                                  1  2'JCRDQE' COLOR(BLU)
     A                                  1 23'Data Queue Entries Display'
     A                                      DSPATR(HI)
     A            SCDOW          9A  O  1 62COLOR(BLU)
     A                                  1 72DATE EDTCDE(Y) COLOR(BLU)
     A                                  2  2'Name:'
     A            SCOBJHEAD     63   O  2  8
     A                                  2 72SYSNAME COLOR(BLU)
     A  31 04N05                        3  2'ENTRIES'
     A                                      DSPATR(HI)
     A  31 04 05                        3  2'KEYS'
     A                                      DSPATR(HI)
     A                                  3 35'Len:'
     A            SENTRYLEN      5Y 0O  3 40EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  3 48'Seq:'
     A            SACCESSTYP    17A  O  3 53DSPATR(HI)
     A                                  4  2'Date    ' DSPATR(UL HI)
     A                                  4 11'Time    ' DSPATR(UL HI)
     A            SCRULER       58A  O  4 21DSPATR(UL HI)
      *----------------------------------------------------------------
     A          R SBFDTA1                   SFL
     A            QUDATE         8A  O  5  2
     A            QUTIME         8A  O  5 11
     A            VIEWQ1        58A  O  5 21
     A            VIEWQ2        58A  O  6 21
      *----------------------------------------------------------------
     A          R SBFCTL1                   SFLCTL(SBFDTA1)
     A  *DS3                                SFLSIZ(0008)
     A  *DS4                                SFLSIZ(0008)
     A  *DS3                                SFLPAG(0007)
     A  *DS4                                SFLPAG(0007)
     A                                      OVERLAY
     A                                      SFLMODE(&VSFLMODE)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A  06                                  SFLDROP(CA13)
     A N06                                  SFLFOLD(CA13)
     A            VSFLMODE       1A  H
     A            VSRECNUM       4S 0H      SFLRCDNBR
     A                                 20  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 21  2'Position to Entry:'
     A            VENTNUM        9Y 0B 21 21EDTCDE(4)
     A                                      DSPATR(HI)
     A                                      CHANGE(23)
     A            VQTOTCNT       9Y 0O 21 49EDTCDE(4)
     A                                 21 59'Total Queue Entries'
     A                                 22  2'Shift to column:'
     A  31        VDSPPOS        5Y 0B 22 21EDTCDE(4)
     A                                      DSPATR(HI)
     A  31        VPOS           5Y 0O 22 32EDTCDE(4)
     A                                 22 38'Current Column'
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 13'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 26'F6=Last Entry'
     A                                      COLOR(BLU)
     A                                 23 41'F10=Hex'
     A                                      COLOR(BLU)
     A                                 23 51'F11=UnFold/Fold'
     A                                      COLOR(BLU)
     A                                 23 68'F12=Cancel'
     A                                      COLOR(BLU)
     A N31                             24  2'No Entries in data queue.'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A  31 04N05                       24  2'F14=Display Key'
     A                                      COLOR(BLU)
     A  31 04 05                       24  2'F14=Display Entry'
     A                                      COLOR(BLU)
     A                                 24 45'Shift F7=Left'
     A                                      COLOR(BLU)
     A                                 24 62'Shift F8=Right'
     A                                      COLOR(BLU)
